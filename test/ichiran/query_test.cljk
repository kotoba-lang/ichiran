(ns ichiran.query-test
  (:require [clojure.test :refer [deftest is testing]]
            [ichiran.query :as query]
            [ichiran.store :as store]))

(defn- backends [] [["MemStore" (store/seed-db)] ["DatomicStore" (store/datomic-seed-db)]])

(deftest draft-status-and-published?
  (doseq [[label s] (backends)]
    (testing label
      (is (= "none" (query/draft-status s "act-finance-q3")))
      (is (not (query/published? s "act-finance-q3")))

      (store/record-datom! s {:kind :draft :id "act-finance-q3" :value {:status :proposed}})
      (is (= "proposed" (query/draft-status s "act-finance-q3")))
      (is (not (query/published? s "act-finance-q3")) "proposed is not published")

      (store/record-datom! s {:kind :draft :id "act-finance-q3" :value {:status :published}})
      (is (= "published" (query/draft-status s "act-finance-q3")))
      (is (query/published? s "act-finance-q3"))

      (is (= "none" (query/draft-status s "act-never-drafted")))
      (is (not (query/published? s "act-never-drafted")) "deny-by-default for never-drafted artifacts"))))
